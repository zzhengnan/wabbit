// mandel.wb
//
// Draw a Mandelbrot set!

var xmin = -2.0;
var xmax = 1.0;
var ymin = -1.5;
var ymax = 1.5;
var width = 80.0;
var height = 40.0;
var threshhold = 1000;

func in_mandelbrot(x0 float, y0 float, n int) int {
    var x = 0.0;
    var y = 0.0;
    var xtemp float;
    while n > 0 {
        xtemp = ((x*x) - (y*y)) + x0;
        y = ((2.0*x)*y) + y0;
        x = xtemp;
        n = n - 1;
        if (x*x) + (y*y) > 4.0 {
            return 0;
        }
    }
    return 1;
}

func mandel() int {
     var dx = (xmax - xmin)/width;
     var dy = (ymax - ymin)/height;

     var y = ymax;
     var x float;

     while y >= ymin {
         x = xmin;
         while x < xmax {
             if in_mandelbrot(x, y, threshhold) == 1 {
                print '*';
             } else {
                print '.';
             }
             x = x + dx;
         }
         print '\n';
         y = y - dy;
         
     }
     return 0;
}

mandel();
